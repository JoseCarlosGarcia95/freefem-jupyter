{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "load \"msh3\";\n",
    "load \"medit\";\n",
    "load \"ff-Ipopt\";\n",
    "\n",
    "real alpha=0.9;\n",
    "int np=30;\n",
    "mesh Th = square(2*np,np,[2*pi*x,pi*y]);\n",
    "\n",
    "fespace Vh(Th,P1,periodic=[[2,y],[4,y]/*,[1,x],[3,x]*/]);\n",
    "\n",
    "/*\n",
    " * Initial shape definition\n",
    " * outside of the mesh adaptation loop to initialize with the previous optimial shape found on further iterations\n",
    " */\n",
    "\n",
    "Vh startshape=5;//+0.5*(cos(10*x)*sin(10*y)*sin(y));\n",
    "\n",
    "//uz,lz,lm : dual variables storage for the warmstart\n",
    "Vh uz=1.,lz=1.;\n",
    "real[int] lm=[1];\n",
    "\n",
    "\n",
    "int nadapt=3;\n",
    "for(int kkk=0;kkk<nadapt;++kkk) //Mesh adaptation loop\n",
    "{\n",
    "\n",
    "int iter=0;\n",
    "func sin2 = square(sin(y));\n",
    "\n",
    "/*\n",
    " * A function which transform Th in 3d mesh (r=rho)\n",
    " * a point (theta,phi) of Th becomes ( r(theta,phi)*cos(theta)*sin(phi) , r(theta,phi)*sin(theta)*sin(phi) , r(theta,phi)*cos(phi) )\n",
    " * then displays the resulting mesh with medit.\n",
    " * (often crashs if some values of rho are negative or if freefem is compiled with debug)\n",
    " */\n",
    "func int Plot3D(real[int] &rho,string cmm,bool ffplot)\n",
    "{\n",
    "\tVh rhoo;\n",
    "\trhoo[]=rho;\n",
    "\t//mesh sTh = square(np,np/2,[2*pi*x,pi*y]);\n",
    "\t//fespace sVh(sTh,P1);\n",
    "\t//Vh rhoplot=rhoo;\n",
    "\ttry\n",
    "\t{\n",
    "\t\tmesh3 Sphere = movemesh23(Th,transfo=[rhoo(x,y)*cos(x)*sin(y),rhoo(x,y)*sin(x)*sin(y),rhoo(x,y)*cos(y)]);\n",
    "\t\tif(ffplot) plot(Sphere);\n",
    "\t\telse medit(cmm ,Sphere,wait=0);\n",
    "\t}\n",
    "\tcatch(...)\n",
    "\t{\n",
    "\t\tcout << \"cant plot\" << endl;\n",
    "\t}\n",
    "\treturn 1;\n",
    "}\n",
    "\n",
    "\n",
    "/*\n",
    " * Surface computation\n",
    " * Maybe is it possible to use movemesh23 to have the surface function less complicated\n",
    " * However, it would not simplify the gradient and the hessian\n",
    " */\n",
    "func real Area(real[int] &X)\n",
    "{\n",
    "\tVh rho;\n",
    "\trho[] = X;\n",
    "\tVh rho2 = square(rho);\n",
    "\tVh rho4 = square(rho2);\n",
    "\treal res = int2d(Th)(sqrt( rho4*sin2 + rho2*square(dx(rho)) + rho2*sin2*square(dy(rho)) ) );\n",
    "\t++iter;\n",
    "\tif(1) plot(rho,value=1,fill=1,cmm=\"rho(theta,phi) on [0,2pi]x[0,pi] - S=\"+res,dim=3);\n",
    "\telse Plot3D(rho[],\"shape_evolution\",1);\n",
    "\treturn res;\n",
    "}\n",
    "func real[int] GradArea(real[int] &X)\n",
    "{\n",
    "\tVh rho,rho2;\n",
    "\trho[] = X;\n",
    "\trho2[] = square(X);\n",
    "\tVh sqrtPsi,alpha;\n",
    "\t{\n",
    "\t\tVh  dxrho2 = dx(rho)*dx(rho), dyrho2 = dy(rho)*dy(rho);\n",
    "\t\tsqrtPsi = sqrt( rho2*rho2*sin2 + rho2*dxrho2 + rho2*dyrho2*sin2 );\n",
    "\t\talpha = 2.*rho2*rho*sin2 + rho*dxrho2 + rho*dyrho2*sin2;\n",
    "\t}\n",
    "\tvarf dArea(u,v) = int2d(Th)(1./sqrtPsi * (alpha*v + rho2*dx(rho)*dx(v) + rho2*dy(rho)*sin2*dy(v)) );\n",
    "\treal[int] grad = dArea(0,Vh);\n",
    "\treturn grad;\n",
    "}\n",
    "matrix hessianA;\n",
    "func matrix HessianArea(real[int] &X)\n",
    "{\n",
    "\tVh rho,rho2;\n",
    "\trho[] = X;\n",
    "\trho2 = square(rho);\n",
    "\tVh sqrtPsi,sqrtPsi3,C00,C01,C02,C11,C12,C22,A;\n",
    "\t{\n",
    "\t\tVh C0,C1,C2;\n",
    "\t\tVh  dxrho2 = dx(rho)*dx(rho), dyrho2 = dy(rho)*dy(rho);\n",
    "\t\tsqrtPsi = sqrt( rho2*rho2*sin2 + rho2*dxrho2 + rho2*dyrho2*sin2 );\n",
    "\t\tsqrtPsi3 = ( rho2*rho2*sin2 + rho2*dxrho2 + rho2*dyrho2*sin2 )*sqrtPsi;\n",
    "\t\tC0 = 2*rho2*rho*sin2 + rho*dxrho2 + rho*dyrho2*sin2;\n",
    "\t\tC1 = rho2*dx(rho);\n",
    "\t\tC2 = rho2*sin2*dy(rho);\n",
    "\t\tC00 = square(C0);\n",
    "\t\tC01 = C0*C1;\n",
    "\t\tC02 = C0*C2;\n",
    "\t\tC11 = square(C1);\n",
    "\t\tC12 = C1*C2;\n",
    "\t\tC22 = square(C2);\n",
    "\t\tA = 6.*rho2*sin2 + dxrho2 + dyrho2*sin2;\n",
    "\t}\n",
    "\tvarf d2Area(w,v) =\n",
    "\t\tint2d(Th)(1./sqrtPsi * (A*w*v + 2*rho*dx(rho)*dx(w)*v + 2*rho*dx(rho)*w*dx(v) + 2*rho*dy(rho)*sin2*dy(w)*v + 2*rho*dy(rho)*sin2*w*dy(v)\n",
    "\t\t\t\t\t\t\t\t+ rho2*dx(w)*dx(v) + rho2*sin2*dy(w)*dy(v)) \n",
    "\t\t+        1./sqrtPsi3 * (C00*w*v + C01*dx(w)*v + C01*w*dx(v) + C02*dy(w)*v + C02*w*dy(v) + C11*dx(w)*dx(v)\n",
    "\t\t\t\t\t\t\t\t   + C12*dx(w)*dy(v) + C12*dy(w)*dx(v) + C22*dy(w)*dy(v)) );\n",
    "\thessianA = d2Area(Vh,Vh);\n",
    "\treturn hessianA;\n",
    "}\n",
    "\n",
    "\n",
    "/*\n",
    " * Volume computation\n",
    " */\n",
    "func real Volume(real[int] &X)\n",
    "{\n",
    "\tVh rho;\n",
    "\trho[]=X;\n",
    "\tVh rho3=rho*rho*rho;\n",
    "\treal res = 1./3.*int2d(Th)(rho3*sin(y));\n",
    "\treturn res;\n",
    "}\n",
    "func real[int] GradVolume(real[int] &X)\n",
    "{\n",
    "\tVh rho;\n",
    "\trho[]=X;\n",
    "\tvarf dVolume(u,v) = int2d(Th)(rho*rho*sin(y)*v);\n",
    "\treal[int] grad = dVolume(0,Vh);\n",
    "\treturn grad;\n",
    "}\n",
    "matrix hessianV;\n",
    "func matrix HessianVolume(real[int] &X)\n",
    "{\n",
    "\tVh rho;\n",
    "\trho[]=X;\n",
    "\tvarf d2Volume(w,v) = int2d(Th)(2*rho*sin(y)*v*w);\n",
    "\thessianV = d2Volume(Vh,Vh);\n",
    "\treturn hessianV;\n",
    "}\n",
    "\n",
    "\n",
    "\n",
    "/*\n",
    " * If we want to use the volume as a constraint function\n",
    " * we must wrap it in some freefem functions returning the appropriate type \n",
    " * The lagrangian hessian also have to be wrapped since the Volume is not linear with \n",
    " * respect to rho, it will constribbute to the hessian.\n",
    " */\n",
    "func real[int] ipVolume(real[int] &X) {real[int] vol = [Volume(X)]; return vol;}\n",
    "matrix mdV;\n",
    "func matrix ipGradVolume(real[int] &X) {real[int,int] dvol(1,Vh.ndof); dvol(0,:)=GradVolume(X); mdV=dvol; return mdV;}\n",
    "matrix HLagrangian;\n",
    "func matrix ipHessianLag(real[int] &X,real objfact,real[int] &lambda)\n",
    "{\n",
    "\tHLagrangian = objfact*HessianArea(X) + lambda[0]*HessianVolume(X);\n",
    "\treturn HLagrangian;\n",
    "}\n",
    "/*building struct for GradVolume */\n",
    "int[int] gvi(Vh.ndof),gvj=0:Vh.ndof-1;\n",
    "gvi=0;\n",
    "\n",
    "\n",
    "/*\n",
    " * The starting value\n",
    " */\n",
    "Vh rc=startshape ;\n",
    "\n",
    "\n",
    "/*\n",
    " * Bounds definition\n",
    " */\n",
    "Vh ub=1.e19;\n",
    "//Vh lb=1.,ub=3.;\n",
    "\n",
    "\n",
    "/*\n",
    " * make it funnier by puptting some objects inside the shape , where rho has to be greater than a given value\n",
    " */\n",
    "Vh lb=0;\n",
    "int shapeswitch=1;\n",
    "real sigma=2*pi/40.,treshold=0.1;\n",
    "func real Gaussian(real X,real Y,real theta,real phi)\n",
    "{\n",
    "\treal deltax2 = square((X-theta)*sin(Y)) , deltay2 = square(Y-phi);\n",
    "\treturn exp(-0.5 * (deltax2 + deltay2) / (sigma*sigma));\n",
    "}\n",
    "real e=0.1,r0=0.25,rr=2-r0;\n",
    "real E=1./(e*e),RR=1./(rr*rr);\n",
    "func disc1 = sqrt(1./(RR+(E-RR)*cos(y)*cos(y)))*(1+0.1*cos(7*x));\n",
    "func disc2 = sqrt(1./(RR+(E-RR)*cos(x)*cos(x)*sin2))  ;\n",
    "if(1) //doesn't work well...\n",
    "{\n",
    "\tlb=r0;\n",
    "\t/*{\n",
    "\t\tfunc f = rr*Gaussian(x,y,0,0);\n",
    "\t\tfunc g = rr*Gaussian(x,y,0,pi);\n",
    "\t\tlb = max(max(lb,f),g);\n",
    "\t}*/\n",
    "\tfor(int q=0;q<5;++q)\n",
    "\t{\n",
    "\t\tfunc f = rr*Gaussian(x,y,2*q*pi/5.,pi/3.);\n",
    "\t\tfunc g = rr*Gaussian(x,y,2*q*pi/5.+pi/5.,2.*pi/3.);\n",
    "\t\tlb = max(max(lb,f),g);\n",
    "\t}\n",
    "\tlb = max(lb,rr*Gaussian(x,y,2*pi,pi/3));\n",
    "}\n",
    "lb =  max(lb,max(disc1, disc2));\n",
    "real Vobj = Volume(lb[]);\n",
    "real Vnvc = 4./3.*pi*pow(lb[].linfty,3);\n",
    "\n",
    "if(1) Plot3D(lb[],\"object_inside\",0);\n",
    "real[int] clb=0.,cub=[(1-alpha)*Vobj + alpha*Vnvc];\n",
    "\n",
    "\n",
    "/*\n",
    " * Call to IPOPT\n",
    " */\n",
    " \n",
    "IPOPT(Area,GradArea,ipHessianLag,ipVolume,ipGradVolume,rc[],ub=ub[],lb=lb[],clb=clb,cub=cub,checkindex=1,maxiter=kkk<nadapt-1 ? 40:150,warmstart=kkk,lm=lm,uz=uz[],lz=lz[],tol=0.00001,structjacc=[gvi,gvj]);\n",
    "\n",
    "/*\n",
    " * Display the final mesh\n",
    " */\n",
    "Plot3D(rc[],\"Shape_at_\"+kkk,0);\n",
    "Plot3D(GradArea(rc[]),\"ShapeGradient\",0);\n",
    "if(kkk<nadapt-1)\n",
    "{\n",
    "\tTh = adaptmesh(Th,rc*cos(x)*sin(y),rc*sin(x)*sin(y),rc*cos(y)/*,hmax=pi/np*/,nbvx=50000,periodic=[[2,y],[4,y]/*,[1,x],[3,x]*/]);\n",
    "\tplot(Th,wait=1);\n",
    "\tstartshape = rc;\n",
    "\tuz=uz;//dual variables interpolation\n",
    "\tlz=lz;\n",
    "}\n",
    "}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "FreeFem++",
   "language": "FreeFem++",
   "name": "freefemjupy"
  },
  "language_info": {
   "mimetype": "text/x-freefem",
   "name": "c++",
   "pygments_lexer": "c++",
   "version": "1.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
